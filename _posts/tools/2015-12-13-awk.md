---
layout: post
title: "awk基础总结和实战"
description: "awk基础总结和实战"
category: "awk"
tags: [awk]
---

在上一篇[**sed基础总结和实战**](http://beginman.cn/sed/2015/12/13/sed/)是针对行的文本处理，这节要总结的`awk`则是针对列的文本处理。它的工作方式：**按行读取文本，并视为一条记录，每条记录以字段分隔为若干字段，然后输出各字段的值。**

实际上awk是一门十分复杂的编程语言，这里只掌握基础的，常用的。

刚才说每条记录按字段分隔，所以awk应该应用于文本格式基本一致的，分隔后把每个非空白的部分叫“域”，从左到右分别用`$1,$2,..$n`表示，`$0`则表示全部域。

下面实例以a.txt为对象进行测试：

	➜  ~  cat a.txt 
	john.wang	Male	30	021-111111
	lucy.yang	Female	25	021-222222
	jack.chen	Male	35	021-333333
	lily.gong	Female	20	021-444444	Shanghai

## 打印指定域

	➜  ~  awk '{print $1,$2,$5}' a.txt
	john.wang Male 
	lucy.yang Female 
	jack.chen Male 
	lily.gong Female Shanghai

其中单引号中的被大括号括着的就是awk的语句，注意，**其只能被单引号包含。**

awk的格式化输出，和C语言的printf一样:

	➜  ~  awk '{printf "%-12s %-10s %-8s\n",$1,$2,$3}'  a.txt
	john.wang    Male       30      
	lucy.yang    Female     25      
	jack.chen    Male       35      
	lily.gong    Female     20      

## 指定打印分隔符
**默认情况下，awk使用空白字符作为分隔符打印的**，也可使用`-F`参数指定分隔符。

如下以`.`为分隔符打印：

	➜  ~  awk -F . '{print $1,$2}' a.txt
	john wang	Male	30	021-111111
	lucy yang	Female	25	021-222222
	jack chen	Male	35	021-333333
	lily gong	Female	20	021-444444	Shanghai

我们常用于处理nginx日志，如下nginx日志配置如下：

	access_log /mnt/logs/nginx/nginx-access.log;
    log_format new '$remote_addr^$http_x_forwarded_for^$host^$time_local^$status^'
    			'$upstream_response_time^$request_time^$request_length^$bytes_sent^$http_referer^$request^$http_user_agent';

 如上配置以`^`为连接符，记录的nginx日志如下：第6，7列分别表示响应和请求时间耗时， 第11列表示URI：

 	175.167.136.17^-^122.27.253.34^13/Dec/2015:10:24:25 +0800^200^0.001^0.001^371^206^-^POST /api/app/in HTTP/1.1^com.lingzhixun.team/2882 (unknown, iPhone OS 9.1, iPhone, Scale/3.000000)
	175.167.136.17^-^122.27.253.34^13/Dec/2015:10:24:33 +0800^200^0.001^0.001^412^206^-^POST /api/app/out HTTP/1.1^com.lingzhixun.team/2882 (unknown, iPhone OS 9.1, iPhone, Scale/3.000000)


我们往往可以查那些请求最耗时，如下：

	cat yusers_access.log| awk -F ^ '{print $7 $11}'|sort
	0.059POST /api/user/register HTTP/1.1
	0.091POST /api/user/send_code HTTP/1.1
	0.093POST /api/user/send_code HTTP/1.1
	0.108POST /api/app/out HTTP/1.1
	0.200POST /api/user/sync_contacts HTTP/1.1


## 截取字符串
`substr()`可以对指定域截取字符串，如将a.txt截取第一个域的第六个字符到最后一个字符，如：

	➜  ~  cat a.txt|awk '{print substr($1, 6)}'
	wang
	yang
	chen
	gong

## 确定字符串长度
内部变量`length`

	➜  ~  cat a.txt|awk '{print length}'        
	28
	30
	28
	39

## 过滤记录

如下过滤第三列年龄大于等于30且为男性的记录：

	➜  ~  awk '$3>=30 && $2=="Male"' a.txt
	john.wang	Male	30	021-111111
	jack.chen	Male	35	021-333333

其中的“==”为比较运算符。其他比较运算符：`!=`, `>`, `<`, `>=`, `<=`, 字符串则用双引号括起。

## 内部变量NF
如果每行列数不一致则，可以通过`NF`变量查看每行的列数

	#使用默认分隔符
	➜  ~  awk '{print NF}' a.txt 
	4
	4
	4
	5

	#指定分隔符
	➜  ~  awk -F . '{print NF}' a.txt
	2
	2
	2
	2

如果加上`$NF`则表示最后一列，`$(NF-1)`打印倒数第二列

	➜  ~  awk '{print $NF}' a.txt
	021-111111
	021-222222
	021-333333
	Shanghai

	➜  ~  awk '{print $(NF-1)}' a.txt
	30
	25
	35
	021-444444

如下awk内建变量：

![](http://beginman.qiniudn.com/awk.png)

## awk脚本

- BEGIN{ 这里面放的是执行前的语句 }
- END {这里面放的是处理完所有的行后要执行的语句 }
- {这里面放的是处理每一行时要执行的语句}

假设有这么一个文件（学生成绩表）

	$ cat score.txt
	Marry   2143 78 84 77
	Jack    2321 66 78 45
	Tom     2122 48 77 71
	Mike    2537 87 97 95
	Bob     2415 40 57 62

	$ cat cal.awk
	#!/bin/awk -f
	#运行前
	BEGIN {
	    math = 0
	    english = 0
	    computer = 0
	 
	    printf "NAME    NO.   MATH  ENGLISH  COMPUTER   TOTAL\n"
	    printf "---------------------------------------------\n"
	}
	#运行中
	{
	    math+=$3
	    english+=$4
	    computer+=$5
	    printf "%-6s %-6s %4d %8d %8d %8d\n", $1, $2, $3,$4,$5, $3+$4+$5
	}
	#运行后
	END {
	    printf "---------------------------------------------\n"
	    printf "  TOTAL:%10d %8d %8d \n", math, english, computer
	    printf "AVERAGE:%10.2f %8.2f %8.2f\n", math/NR, english/NR, computer/NR
	}

我们来看一下执行结果：（也可以这样运行 ./cal.awk score.txt）

	➜  ~  awk -f cal.awk score.txt 
	NAME    NO.   MATH  ENGLISH  COMPUTER   TOTAL
	---------------------------------------------
	Marry  2143     78       84       77      239
	Jack   2321     66       78       45      189
	Tom    2122     48       77       71      196
	Mike   2537     87       97       95      279
	Bob    2415     40       57       62      159
	---------------------------------------------
	  TOTAL:       319      393      350 
	AVERAGE:     63.80    78.60    70.00

## 常用范例

计算所有的C文件，CPP文件和H文件的文件大小总和

	$ ls -l  *.cpp *.c *.h | awk '{sum+=$5} END {print sum}'
	2511401

统计各个netstat状态的用法

	$ netstat -n | awk '/^tcp/ {++S[$NF]} END {for(a in S) print a, S[a]}'
	
	TIME_WAIT 10
	CLOSE_WAIT 39
	ESTABLISHED 356

统计每个用户的进程的占了多少内存（注：sum的RSS那一列）

	$ ps aux | awk 'NR!=1{a[$1]+=$6;} END { for(i in a) print i ", " a[i]"KB";}'
	ntp, 940KB
	mysql, 55108KB
	team, 3656KB
	497, 764KB
	nscd, 1060KB
	nginx, 111568KB
	mongod, 183972KB
	root, 1399096KB

从file文件中找出长度大于80的行

	awk 'length>80' file

按连接数查看客户端IP

	netstat -ntu | awk '{print $5}' | cut -d: -f1 | sort | uniq -c | sort -nr

打印99乘法表

	$ seq 9 | sed 'H;g' | awk -v RS='' '{for(i=1;i<=NF;i++)printf("%dx%d=%d%s", i, NR, i*NR, i==NR?"\n":"\t")}'
	1x1=1
	1x2=2	2x2=4
	1x3=3	2x3=6	3x3=9
	1x4=4	2x4=8	3x4=12	4x4=16
	1x5=5	2x5=10	3x5=15	4x5=20	5x5=25
	1x6=6	2x6=12	3x6=18	4x6=24	5x6=30	6x6=36
	1x7=7	2x7=14	3x7=21	4x7=28	5x7=35	6x7=42	7x7=49
	1x8=8	2x8=16	3x8=24	4x8=32	5x8=40	6x8=48	7x8=56	8x8=64
	1x9=9	2x9=18	3x9=27	4x9=36	5x9=45	6x9=54	7x9=63	8x9=72	9x9=81


参考：

- [《Linux系统命令及Shell脚本实践指南》]
- [AWK 简明教程](http://coolshell.cn/articles/9070.html)
